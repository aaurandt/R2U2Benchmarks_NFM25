INPUT
    x_acc, y_acc, z_acc, vert_acc, vert_velocity, alt, pressure, temperature, time, state_1_time: float;
    actuation_status: bool;
    rocket_state: int;
    
ATOMIC
    inLaunchPadState := rocket_state == 0;
    inBoostState := rocket_state == 1;
    inCoastState := rocket_state == 2;
    inDescentState := rocket_state == 3;
    altBelowMax := alt < 10780.0;
    altAboveMin := alt > 2150.0;
    timeBelowMax := time < 45500.0;
    timeAboveMin := time > 6330.0;
    ninetyPercentBurnTime := state_1_time > 5700.0;
    vertVelocityBelowMax := vert_velocity < 536.0;
    vertVelocityAboveZero := vert_velocity > 0.0;
    vertVelocityAboveThreshold := vert_velocity > 100.0;
    vertAccBelowBoostMax := vert_acc < 129.0;
    vertAccBelowCoastMax := vert_acc <= 0.0;
    vertAccBelowZero := vert_acc < 0.0;
    vertAccAboveZero := vert_acc > 0.0;
    tempBelowThresholdRC_1 := rate(temperature) > -0.05;
    tempBelowThresholdRC_2 := rate(temperature) < 0.05;
    presBelowThresholdRC_1 := rate(pressure) > -250.0;
    presBelowThresholdRC_2 := rate(pressure) < 250.0;
    presBelowMaxRC_1 := rate(pressure) > -500.0;
    presBelowMaxRC_2 := rate(pressure) < 500.0;
    timeBelowThresholdRC_1 := rate(pressure) > -55.0;
    timeBelowThresholdRC_2 := rate(pressure) < 55.0;
    noChangeVertAcc := rate(vert_acc) == 0.0;
    noChangeVertVelocity := rate(vert_velocity) == 0.0;
    -- accAngleAboveThreshold := 
    -- accMagnitudeAboveThreshold :=

FTSPEC
    SPEC_OR_1: altBelowMax && (actuation_status -> altAboveMin);
    SPEC_OR_2: actuation_status -> (timeBelowMax && timeAboveMin);
    SPEC_OR_3: vertVelocityBelowMax;
    SPEC_OR_4: (inLaunchPadState || inBoostState || inCoastState) -> vertVelocityAboveZero;
    SPEC_OR_5: inBoostState -> vertAccBelowBoostMax;
    SPEC_OR_6: inCoastState -> vertAccBelowCoastMax;
    SPEC_RC_1: !G[0,2]!(tempBelowThresholdRC_1 && tempBelowThresholdRC_2);
    SPEC_RC_2: !G[0,2]!(presBelowThresholdRC_1 && presBelowThresholdRC_2);
    SPEC_RC_3: presBelowMaxRC_1 && presBelowMaxRC_2;
    SPEC_RC_4: !G[0,2]!(timeBelowThresholdRC_1 && timeBelowThresholdRC_2);
    SPEC_RC_5: !G[0,2]!(noChangeVertAcc);
    SPEC_RC_6: !G[0,2]!(noChangeVertVelocity);
    SPEC_CS_1: inBoostState -> F[0,140] inCoastState;
    -- SPEC_CS_2: inCoastState -> F[0,800] inDescentState;
    -- SPEC_CS_3: (accAngleAboveThreshold && inBoostState) -> F[0,10] inCoastState;
    SPEC_CS_4: inBoostState -> (inBoostState U[0,130] vertAccBelowZero);
    -- SPEC_CS_5: actuation_status -> F[0,5] accMagnitudeAboveThreshold;
    SPEC_CS_6: (inBoostState && vertVelocityAboveThreshold) -> F[0,126] vertAccAboveZero;
    SPEC_CS_7: inBoostState -> (inBoostState U[0,114] ninetyPercentBurnTime);
